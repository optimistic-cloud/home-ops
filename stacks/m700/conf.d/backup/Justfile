export RESTIC_DOCKER_IMAGE := "restic/restic:0.18.1"

[private]
_run app type +args:
	#!/usr/bin/env bash
	set -euo pipefail

	if [[ "{{type}}" != "local" && "{{type}}" != "onsite" && "{{type}}" != "offsite" ]]; then
		echo "Invalid type '{{type}}'. Use: local, onsite, or offsite." >&2
		exit 1
	fi

	env_file="../{{app}}.{{type}}.restic.env"
	if [[ ! -f "$env_file" ]]; then
		echo "Env file not found: $env_file" >&2
		exit 1
	fi

	docker run --rm \
		--env-file "$env_file" \
		"{{RESTIC_DOCKER_IMAGE}}" {{args}}

snapshots app type:
	just _run {{app}} {{type}} snapshots

stats app type:
	just _run {{app}} {{type}} stats

forget-prune app type keep_last="7" keep_daily="7" keep_weekly="4" keep_monthly="6":
	just _run {{app}} {{type}} forget --keep-last {{keep_last}} --keep-daily {{keep_daily}} --keep-weekly {{keep_weekly}} --keep-monthly {{keep_monthly}} --prune

prune app type:
	just _run {{app}} {{type}} prune

unlock app type:
	just _run {{app}} {{type}} unlock
