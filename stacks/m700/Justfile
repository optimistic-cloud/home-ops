mod backup './conf.d/backup/scripts'

alias u := update
alias d := down

help:
    just --list --list-submodules

[group('docker')]
[doc('Update the stack')]
update: down && up
    git pull origin main

[group('docker')]
[doc('Start the stack')]
up:
    docker compose up -d

[group('docker')]
[doc('Stop the stack')]
down:
    docker compose down

[group('docker')]
[doc('Get logs for a service')]
logs service:
    docker compose logs -f {{service}}

[group('restic')]
[doc('Init repository')]
init app provider:
    docker exec -it backup-toolkit bar.sh init {{app}} {{provider}}

[group('restic')]
[doc('Backup application')]
abackup app provider:
    just --justfile conf.d/backup/scripts/.justfile backup {{app}} {{provider}}

[group('restic')]
[doc('Restore backup for an application')]
raestore app provider:
    just --justfile conf.d/backup/scripts/.justfile backup {{app}} {{provider}}
