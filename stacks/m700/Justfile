# info, debug, warn, error
export NU_LOG_LEVEL := "info"

alias u := update
alias d := down

help:
    just --list --list-submodules

[group('docker')]
[doc('Update the stack')]
update: down && up
    git pull origin main

[group('docker')]
[doc('Start the stack')]
up:
    docker compose up -d

[group('docker')]
[doc('Stop the stack')]
down:
    docker compose down

[group('docker')]
[doc('Get logs for a service')]
logs service:
    docker compose logs -f {{service}}

[group('restic')]
[doc('Init repository')]
[working-directory: 'conf.d/backup/scripts/']
init app provider:
    nu {{app}}.nu init --provider {{provider}}

[group('restic')]
[doc('Get snapshots')]
[working-directory: 'conf.d/backup/scripts/']
init app provider:
    nu {{app}}.nu snapshot --provider {{provider}}

[group('restic')]
[doc('List files in the snapshot')]
[working-directory: 'conf.d/backup/scripts/']
ls app provider:
    nu {{app}}.nu ls --provider {{provider}}

[group('restic')]
[doc('Get stats of the repository')]
[working-directory: 'conf.d/backup/scripts/']
stats app provider:
    nu {{app}}.nu stats --provider {{provider}}

[group('restic')]
[doc('Backup application')]
[working-directory: 'conf.d/backup/scripts/']
backup app provider:
    nu {{app}}.nu --provider {{provider}}

[group('restic')]
[doc('Restore backup for an application')]
[working-directory: 'conf.d/backup/scripts/']
restore app provider:
    nu {{app}}.nu restore --provider {{provider}}