# info, debug, warn, error
export NU_LOG_LEVEL := "info"

alias u := update
alias d := down

help:
    just --list --list-submodules

[group('docker')]
[doc('Update the stack')]
update: down && up
    git pull origin main

[group('docker')]
[doc('Start the stack')]
up:
    docker compose up -d

[group('docker')]
[doc('Stop the stack')]
down:
    docker compose down

[group('docker')]
[doc('Get logs for a service')]
logs service:
    docker compose logs -f {{service}}

[group('restic')]
[doc('Init repository')]
init app provider:
    nu ./conf.d/backup/restic.nu init --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env

[group('restic')]
[doc('Get snapshots')]
snapshots app provider:
    nu ./conf.d/backup/restic.nu snapshot --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env

[group('restic')]
[doc('List files in the snapshot')]
ls app provider:
    nu ./conf.d/backup/restic.nu --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env

[group('restic')]
[doc('Get stats of the repository')]
stats app provider:
    nu ./conf.d/backup/restic.nu --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env

[group('restic')]
[doc('Backup application')]
backup app provider:
    nu ./conf.d/backup/{{app}}.nu --env-file ./conf.d/backup/env.toml --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env

[group('restic')]
[doc('Restore backup for an application')]
restore app provider restore_path:
    nu ./conf.d/backup/restic.nu restore --provider-env-file ./conf.d/backup/{{app}}.{{provider}}.restic.env --restore-path {{restore_path}}
